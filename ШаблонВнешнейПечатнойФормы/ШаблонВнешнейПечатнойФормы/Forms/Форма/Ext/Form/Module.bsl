#Область ОбработчикиКомандформы

&НаКлиенте
Процедура Печать(Команда)
	
	КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм("ШаблонВнешнейПечатнойФормы");
	
	ПечатьНаСервере(КоллекцияПечатныхФорм);
		
	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПечатьНаСервере(КоллекцияПечатныхФорм)
	
	КоллекцияПечатныхФормТЗ = МассивСтруктурВТаблицуЗначений(КоллекцияПечатныхФорм);
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(СсылкаНаОбъект);
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.Печать(МассивОбъектов,
		КоллекцияПечатныхФормТЗ,
		Новый СписокЗначений,
		Неопределено);
		
	КоллекцияПечатныхФорм = ОбщегоНазначения.ТаблицаЗначенийВМассив(КоллекцияПечатныхФормТЗ);
	
КонецПроцедуры

Функция МассивСтруктурВТаблицуЗначений(МассивСтруктур)
    
    Результат = Новый ТаблицаЗначений;
    
    Для Каждого Строка Из МассивСтруктур Цикл
        Для Каждого КлючЗначение Из Строка Цикл
            Если Результат.Колонки.Найти(КлючЗначение.Ключ) = Неопределено  Тогда
                Результат.Колонки.Добавить(КлючЗначение.Ключ);
            КонецЕсли; 
        КонецЦикла;
        ЗаполнитьЗначенияСвойств(Результат.Добавить(), Строка);
    КонецЦикла;
        
    Возврат Результат;
	
КонецФункции

#КонецОбласти